#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

#####################################################################################
# Packages management
####################################################################################
# This files contains utilities bundles to install package or check if they are
# installed, optionnaly in a specific version.
#
# It contains four bundles:
# - install_package:         install or update a package in the latest version available
# - install_package_version: install or update a package in a specific version
# - verify_package:          verify if a package in installed
# - verify_package_version:  verify is a package in installed in a specific version
#
#Â Please take a look at the corresponding bundles for extended usage details
####################################################################################


####################################################################################
# install_package: Install or update a package in its latest version available
# Contract:
#  - Args:
#     -- "package_name", the name of the package to install
#  - Returns: defines a class install_package_${package_name}_{kept,repaired,ok,not_ok,reached}
# Usage example:
# methods:
#     "any" usebundle => install_package("nrpe");
####################################################################################
bundle agent install_package(package_name)
{
  methods:
      "placeholder" usebundle => install_package_version("${package_name}", "latest");
}

####################################################################################
# install_package_version: Install or update a package in a specific version
# Contract:
#  - Args:
#     -- "package_name", the name of the package to install
#     -- "package_version", version of the package to install (can be "latest" to install it
#                           in its latest version)
#  - Returns: defines a class install_package_${package_name}_{kept,repaired,ok,not_ok,reached}
# Usage example:
# methods:
#     "any" usebundle => install_package("nrpe", "el6");
####################################################################################
bundle agent install_package_version(package_name, package_version)
{
  methods:
      "placeholder" usebundle => install_package_version_cmp("${package_name}", "==", "${package_version}", "add");
}


####################################################################################
# verify_package: Verify if a package is installed in its latest version available
# Contract:
#  - Args:
#     -- "package_name", the name of the package to verify
#  - Returns: defines a class install_package_${package_name}_{kept,repaired,ok,not_ok,reached}
# Usage example:
# methods:
#     "any" usebundle => verify_package("nrpe");
####################################################################################
bundle agent verify_package(package_name)
{
  methods:
      "placeholder" usebundle => verify_package_version("${package_name}", "latest");
}

####################################################################################
# verify_package_version: Verify if a package is installed in a specific version
# Contract:
#  - Args:
#     -- "package_name", the name of the package to verify
#     -- "package_version", the version of the package to verify (can be "latest" for latest version)
#  - Returns: defines a class install_package_${package_name}_{kept,repaired,ok,not_ok,reached}
# Usage example:
# methods:
#     "any" usebundle => verify_package_version("nrpe", "el6");
####################################################################################
bundle agent verify_package_version(package_name, package_version)
{
  methods:
    "placeholder" usebundle => install_package_version_cmp("${package_name}", "==", "${package_version}", "verify");
}



####################################################################################
# install_package_version_cmp: Install a package or verify if it is installed in a specific version,
# or higher or lower version than a version specified
# Contract:
#  - Args:
#     -- "package_name", name of the package to install or verify
#     -- "version_comparator", comparator between installed version and defined version; can be
#                              "==": exact version
#                              "<=": lower than or equal to the specified version
#                              ">=": greater than or equal to the specified version
#                              ">": greater than the specified version
#                              "<": lower than the specified version
#                              "!=": not equal to the specified version
#     -- "package_version", the version of the package to verify (can be "latest" for latest version)
#     -- "action", action to perform, can be
#                  "add": to install or update the package
#                  "verify": to check if it is installed 
#                  default to "verify"
#  - Returns: defines a class install_package_${package_name}_{kept,repaired,ok,not_ok,reached}
# Usage example:
# methods:
#     "any" usebundle => install_package_version_cmp("postgresql", ">=", "9.1", "verify");
####################################################################################

bundle agent install_package_version_cmp(package_name, version_comparator, package_version, action)
{
  vars:
      "class_prefix" string => "install_package_${package_name}";

  defaults:
      "action" string => "immediate", if_match_regex => "add";
      "action" string => "warn_only", if_match_regex => "verify|";
 
  classes:
      "local_package" expression => regcmp("(http:|file:)?/.*", "${package_name}");
      "version_not_specified" expression => strcmp("latest", "${package_version}");
      "immediate" expression => strcmp("immediate", "${action}");

  packages:
    !version_not_specified.local_package.redhat::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => redhat_local_install,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    !version_not_specified.!local_package.redhat::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => yum_rpm,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.redhat::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => redhat_local_install,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;


    version_not_specified.!local_package.redhat::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => yum_rpm,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;


    !version_not_specified.local_package.debian::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => debian_local_install,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    !version_not_specified.!local_package.debian::
      "${package_name}"
                        package_policy  => "addupdate",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => apt_get,
                        classes         => classes_generic("${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.debian::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => debian_local_install,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;


    version_not_specified.!local_package.debian::
      "${package_name}"
                        package_policy => "addupdate",
                        package_method => apt_get,
                        classes        => classes_generic("${class_prefix}"),
                        action         => do_if_immediate;


  methods:
    immediate::
      "Reports" usebundle => logger("${this.promise_filename}", "Install package ${package_name} in version ${package_version}", "${class_prefix}");
    !immediate::
      "Reports" usebundle => logger("${this.promise_filename}", "Verify that package ${package_name} is installed in version ${package_version}", "${class_prefix}");
}
