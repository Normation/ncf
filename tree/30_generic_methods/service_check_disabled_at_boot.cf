#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Service check disabled at boot
# @description Check if a service is set to not start at boot using the appropriate method
#
# @parameter name  Service name (as recognized by systemd, init.d, etc...)
# 
#
# @parameter_rename service_name name
# @class_prefix service_check_disabled_at_boot
# @class_parameter name
# This bundle will define a condition service_check_disabled_at_boot_${name}_{kept,ok,not_ok,failed,reached}

bundle agent service_check_disabled_at_boot(name)
{
  vars:
      "canonified_name"         string => canonify("${name}");

      "old_class_prefix"        string => "service_check_disabled_at_boot_${canonified_name}";

      "args"                     slist => { "${name}" };
      "report_param"            string => join("_", args);
      "full_class_prefix"       string => canonify("service_check_disabled_at_boot_${report_param}");
      "class_prefix"            string => string_head("${full_class_prefix}", "1000");

      "full_inner_class_prefix" string => canonify("ncf_services_${report_param}_is_enabled");
      "inner_class_prefix"      string => string_head("${full_inner_class_prefix}", "1000");



  classes:
      "should_report"       expression => "${report_data.should_report}";

  methods:
      "disable_reporting_${class_prefix}"
                             usebundle => disable_reporting;
      "check_enabled"        usebundle => ncf_services("${name}", "is-enabled");

      "reenable_reporting_${class_prefix}"
                             usebundle => enable_reporting,
                            ifvarclass => "should_report";
      "success"              usebundle => _classes_success("${old_class_prefix}"),
                            ifvarclass => "ncf_services_${canonified_name}_is_enabled_not_ok";
      "success"              usebundle => _classes_success("${class_prefix}"),
                            ifvarclass => "${inner_class_prefix}_not_ok";

      "failure"              usebundle => _classes_failure("${old_class_prefix}"),
                            ifvarclass => "ncf_services_${canonified_name}_is_enabled_ok";
      "failure"              usebundle => _classes_failure("${class_prefix}"),
                            ifvarclass => "${inner_class_prefix}_ok";

      "reports" usebundle => _log_v3("Check if service ${name} is disabled at boot", "${name}", "${old_class_prefix}", "${class_prefix}", @{args});

}
