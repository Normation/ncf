#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name User primary group
# @description Define the shell of the user. User must already exist.
# @documentation This method does not create the user.
#
# @parameter login User's login
# @parameter primary_group User's primary group
# 
# @class_prefix user_primary_group
# @class_parameter login

bundle agent user_primary_group(login, primary_group)
{
  vars:
      "old_class_prefix"  string => canonify("user_primary_group_${login}");
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"               slist => { "${login}", "${primary_group}" };
      
  classes:
      "user_exists" expression => userexists("${login}");
      "group_exists" expression => groupexists("${primary_group}");

      "args_defined" expression => "user_exists.group_exists";

  users:
      "${login}"  policy        => "present",
                  group_primary => "${primary_group}",
                  classes       => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                  if    => "args_defined";

  methods:
    !args_defined::
      "error" usebundle => _classes_failure("${old_class_prefix}");
      "error" usebundle => _classes_failure("${class_prefix}");

      "report_error"
        usebundle => _log("Group ${primary_group} does not exist. Setting user ${login} primary_group to ${primary_group}", "${old_class_prefix}", "${class_prefix}", @{args}),
        if => "!group_exists.user_exists";
      
      "report_error"
        usebundle => _log("User ${login} does not exist. Setting user ${login} primary_group to ${primary_group}", "${old_class_prefix}", "${class_prefix}", @{args}),
        if => "!user_exists.group_exists";

      "report_error"
        usebundle => _log("User ${login} and group ${primary_group} do not exist. Setting user ${login} primary_group to ${primary_group}", "${old_class_prefix}", "${class_prefix}", @{args}),
        if => "!user_exists.!group_exists";

    args_defined:: 
      "report_success"
        usebundle => _log("User ${login} primary_group set to ${primary_group}", "${old_class_prefix}", "${class_prefix}", @{args}),
        if => "(!has_promiser_stack.${old_class_prefix}_reached)|(has_promiser_stack.${class_prefix}_reached)";
}

