#####################################################################################
# Copyright 2016 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File ensure key -> value present
# @description Ensure that the file contains a pair of "key separator value"
# @documentation Edit (or create) the file, and ensure it contains an entry key -> value with arbitrary separator between the key and its value.
# If the key is already present, the method will change the value associated with this key.
#
# @parameter file File name to edit
# @parameter key Key to define
# @parameter value Value to define
# @parameter separator Separator between key and value (for example "=" or " ")
#
# @class_prefix file_ensure_key_value
# @class_parameter file

bundle agent file_ensure_key_value(file, key, value, separator)
{
  vars:
      "old_class_prefix" string => canonify("file_ensure_key_value_${file}");
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"              slist => { "${file}", "${key}", "${value}","${separator}" };

      "keys[${key}]" string => "${value}";

  files:
      "${file}"
        create        => "true",
        edit_line     => ncf_maintain_keys_values("file_ensure_key_value.keys", "${separator}"),
        edit_defaults => ncf_empty_select("false"),
        classes       => classes_generic_two("${old_class_prefix}", "${class_prefix}");

  methods:
      "sanitize" usebundle => _classes_sanitize("${class_prefix}");
      "sanitize" usebundle => _classes_sanitize("${old_class_prefix}");
      "report"   usebundle  => _log("Ensure line in format key${separator}value in ${file}", "${old_class_prefix}", "${class_prefix}", @{args});
}
