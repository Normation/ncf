#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Variable dict merge
# @description Define a variable resulting of the merge of two other variables
# @documentation To use the generated variable, you must use the form `${variable_prefix.variable_name[key]}` with each name replaced with the parameters of this method.
#
# The resulting variable will be the merge of the two parameters, which means it is built by:
#
# * Taking the content of the first variable
# * Adding the content of the second variable, and replace the kays that were already there
#
# It is only a one-level merge, and the value of the first-lvel key will be completely replaced by the merge.
#
#### Usage
#
# If you have a `prefix.variable1` variable defined by:
#
# ```json
# { "key1": "value1", "key2": "value2", "key3": { "keyx": "valuex" } }
# ```
#
# And a `prefix.variable2` variable defined by:
#
# ```json
# { "key1": "different", "key3": "value3", "key4": "value4" }
# ```
#
# And that you use:
#
# ```
# variablr_dict_merge("prefix", "variable3, "prefix.variable1", "prefix.variable2")
# ```
#
# You will get a `prefix.variable3` variable containing:
#
# ```json
# {
#   "key1": "different",
#   "key2": "value2",
#   "key3": "value3",
#   "key4": "value4"
# }
# ```
#
# @parameter variable_prefix The prefix of the variable name
# @parameter variable_name   The variable to define, the full name will be variable_prefix.variable_name
# @parameter first_variable  The first variable, which content will be overriden in the resulting variable if necessary (written in the form variable_prefix.variable_name)
# @parameter second_variable The second variable, which content will override the first in the resulting variable if necessary (written in the form variable_prefix.variable_name)
#
# @agent_version >=3.6
# 
# @class_prefix variable_dict_merge
# @class_parameter variable_name

bundle agent variable_dict_merge(variable_prefix, variable_name, first_variable, second_variable)
{
  vars:
      "old_class_prefix"  string => canonify("variable_dict_merge_${variable_name}");
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"               slist => { "${variable_prefix}", "${variable_name}", "${first_variable}", "${second_variable}" };

      # define the variable within the variable_prefix namespace
      "${variable_prefix}.${variable_name}"  data => mergedata("${first_variable}", "${second_variable}");

  classes:
     "variable_defined" expression => isvariable("${variable_prefix}.${variable_name}");

  methods:
    !variable_defined::
      "error"    usebundle => _classes_failure("${old_class_prefix}");
      "error"    usebundle => _classes_failure("${class_prefix}");

    variable_defined::
      "success"  usebundle => _classes_success("${old_class_prefix}");
      "success"  usebundle => _classes_success("${class_prefix}");

    any::
      "report"
        usebundle  => _log("Set the dict ${variable_prefix}.${variable_name} to the content of the merge of ${first_variable} and ${second_variable}", "${old_class_prefix}", "${class_prefix}", @{args});
}
