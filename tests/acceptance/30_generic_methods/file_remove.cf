#######################################################
#
# Test checking that file_remove removes a file
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"                    string => getenv("TEMP", 1024);
    "destination_file"       string => "${tmp}/remove_test";
    "destination_file_canon" string => canonify("${destination_file}");
    "not_a_file"             string => "${tmp}/nonexistantfilenamefromncftests";
    "not_a_file_canon"       string => canonify("${not_a_file}");

  files:
    "${destination_file}"
      create => "true";

}

#######################################################

bundle agent test
{
  methods:
    "ph1" usebundle => file_remove("${init.destination_file}");
    "ph2" usebundle => file_remove("${init.not_a_file}");
}

#######################################################

bundle agent check
{
  classes:
    "file_exists"      expression => fileexists("${init.destination_file}");
    "ok_1"             expression => "!file_exists.file_remove_${init.destination_file_canon}_repaired.!file_remove_${init.destination_file_canon}_error";
    "ok_2"             expression => "file_remove_${init.not_a_file_canon}_kept.!file_remove_${init.not_a_file_canon}_repaired.!file_remove_${init.not_a_file_canon}_error";
    "ok"                      and => { "ok_1", "ok_2" };

  reports:
    file_exists::
      "First test: File ${init.destination_file} exists even though it should have been removed!";
    ok_1::  "First test OK (removing existing file, ${init.destination_file})";
    !ok_1:: "First test failed (removing existing file, ${init.destination_file})";
    ok_2::  "Second test OK (removing a non existant file, ${init.not_a_file_canon})";
    !ok_2:: "Second test failed (removing a non existant file, ${init.not_a_file_canon})";

    !ok_2:: #debug
      "Second test debug: class file_remove_${init.not_a_file_canon}_reached defined" ifvarclass => "file_remove_${init.not_a_file_canon}_reached";
      "Second test debug: class file_remove_${init.not_a_file_canon}_kept defined" ifvarclass => "file_remove_${init.not_a_file_canon}_kept";
      "Second test debug: class file_remove_${init.not_a_file_canon}_repaired defined" ifvarclass => "file_remove_${init.not_a_file_canon}_repaired";
      "Second test debug: class file_remove_${init.not_a_file_canon}_error defined" ifvarclass => "file_remove_${init.not_a_file_canon}_error";

    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
